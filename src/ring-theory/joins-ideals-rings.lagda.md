# Joins of ideals of rings

```agda
module ring-theory.joins-ideals-rings where
```

<details><summary>Imports</summary>

```agda
open import foundation.unions-subtypes
open import foundation.universe-levels

open import order-theory.least-upper-bounds-large-posets

open import ring-theory.ideals-generated-by-subsets-rings
open import ring-theory.ideals-rings
open import ring-theory.posets-of-ideals-rings
open import ring-theory.rings
```

</details>

## Idea

The **join** of a family of [ideals](ring-theory.ideals-rings.md) of
[rings](ring-theory.rings.md) is the least ideal containing all the ideals in
the family of ideals. In other words, the join of a family of ideals is the
[ideal generated by](ring-theory.ideals-generated-by-subsets-rings.md) the union
of the underlying subsets of the ideals in the family of ideals.

## Definition

### The predicate of being the join of a family of ideals

```agda
module _
  {l1 l2 l3 : Level} (R : Ring l1) (I : UU l2) (J : I → ideal-Ring l3 R)
  where

  is-join-family-of-ideals-Ring :
    {l4 : Level} → ideal-Ring l4 R → UUω
  is-join-family-of-ideals-Ring =
    is-least-upper-bound-family-of-elements-Large-Poset
      ( ideal-Ring-Large-Poset R)
      ( J)
```

### The join of a family of ideals

```agda
module _
  {l1 l2 l3 : Level} (R : Ring l1) (I : UU l2) (J : I → ideal-Ring l3 R)
  where

  join-family-of-ideals-Ring :
    ideal-Ring (l1 ⊔ l2 ⊔ l3) R
  join-family-of-ideals-Ring =
    ideal-subset-Ring R
      ( union-family-of-subtypes (λ i → subset-ideal-Ring R (J i)))
```
