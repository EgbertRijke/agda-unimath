# Ideals generated by subsets of rings

```agda
module ring-theory.ideals-generated-by-subsets-rings where
```

<details><summary>Imports</summary>

```agda
open import foundation.cartesian-product-types
open import foundation.dependent-pair-types
open import foundation.fibers-of-maps
open import foundation.functions
open import foundation.identity-types
open import foundation.propositional-truncations
open import foundation.propositions
open import foundation.subtypes
open import foundation.universe-levels

open import lists.concatenation-lists
open import lists.functoriality-lists
open import lists.lists

open import ring-theory.ideals-rings
open import ring-theory.rings
open import ring-theory.subsets-rings
```

</details>

## Idea

If `S` is a subset of a ring `R`, then the left/right/two-sided ideal generated
by `S` is the least left/right/two-sided ideal containing `S`. This idea
expresses the universal property of the ideal generated by a subset of a ring.
We will construct it as the type of finite linear combinations of elements in
`S`.

## Definitions

### Universal property of left ideals generated by a subset of a ring

```agda
module _
  {l1 l2 l3 : Level} (R : Ring l1) (S : subset-Ring l2 R)
  (I : left-ideal-Ring l3 R) (H : S ⊆ subset-left-ideal-Ring R I)
  where

  is-left-ideal-generated-by-subset-Ring :
    (l : Level) → UU (l1 ⊔ l2 ⊔ l3 ⊔ lsuc l)
  is-left-ideal-generated-by-subset-Ring l =
    (J : left-ideal-Ring l R) →
    S ⊆ subset-left-ideal-Ring R J →
    subset-left-ideal-Ring R I ⊆ subset-left-ideal-Ring R J
```

### Universal property of right ideals generated by a subset of a ring

```agda
module _
  {l1 l2 l3 : Level} (R : Ring l1) (S : subset-Ring l2 R)
  (I : right-ideal-Ring l3 R) (H : S ⊆ subset-right-ideal-Ring R I)
  where

  is-right-ideal-generated-by-subset-Ring :
    (l : Level) → UU (l1 ⊔ l2 ⊔ l3 ⊔ lsuc l)
  is-right-ideal-generated-by-subset-Ring l =
    (J : right-ideal-Ring l R) →
    S ⊆ subset-right-ideal-Ring R J →
    subset-right-ideal-Ring R I ⊆ subset-right-ideal-Ring R J
```

### Universal property of ideals generated by a subset of a ring

```agda
module _
  {l1 l2 l3 : Level} (R : Ring l1) (S : subset-Ring l2 R)
  (I : ideal-Ring l3 R) (H : S ⊆ subset-ideal-Ring R I)
  where

  is-ideal-generated-by-subset-Ring :
    (l : Level) → UU (l1 ⊔ l2 ⊔ l3 ⊔ lsuc l)
  is-ideal-generated-by-subset-Ring l =
    (J : ideal-Ring l R) →
    S ⊆ subset-ideal-Ring R J →
    subset-ideal-Ring R I ⊆ subset-ideal-Ring R J
```

### Construction of left ideals generated by a subset of a ring

```agda
module _
  {l1 l2 : Level} (R : Ring l1) (S : subset-Ring l2 R)
  where

  left-formal-combination-subset-Ring : UU (l1 ⊔ l2)
  left-formal-combination-subset-Ring =
    list (type-Ring R × type-subset-Ring R S)

  ev-left-formal-combination-subset-Ring :
    left-formal-combination-subset-Ring → type-Ring R
  ev-left-formal-combination-subset-Ring nil = zero-Ring R
  ev-left-formal-combination-subset-Ring (cons (pair r s) l) =
    add-Ring R
      ( mul-Ring R r (inclusion-subset-Ring R S s))
      ( ev-left-formal-combination-subset-Ring l)

  preserves-concat-ev-left-formal-combination-subset-Ring :
    (u v : left-formal-combination-subset-Ring) →
    Id ( ev-left-formal-combination-subset-Ring (concat-list u v))
       ( add-Ring R
         ( ev-left-formal-combination-subset-Ring u)
         ( ev-left-formal-combination-subset-Ring v))
  preserves-concat-ev-left-formal-combination-subset-Ring nil v =
    inv (left-unit-law-add-Ring R (ev-left-formal-combination-subset-Ring v))
  preserves-concat-ev-left-formal-combination-subset-Ring
    ( cons (pair r s) u) v =
    ( ap
      ( add-Ring R (mul-Ring R r (inclusion-subset-Ring R S s)))
      ( preserves-concat-ev-left-formal-combination-subset-Ring u v)) ∙
    ( inv
      ( associative-add-Ring R
        ( mul-Ring R r (inclusion-subset-Ring R S s))
        ( ev-left-formal-combination-subset-Ring u)
        ( ev-left-formal-combination-subset-Ring v)))

  mul-left-formal-combination-subset-Ring :
    type-Ring R →
    left-formal-combination-subset-Ring → left-formal-combination-subset-Ring
  mul-left-formal-combination-subset-Ring r =
    map-list (λ x → pair (mul-Ring R r (pr1 x)) (pr2 x))

  preserves-mul-ev-left-formal-combination-subset-Ring :
    (r : type-Ring R) (u : left-formal-combination-subset-Ring) →
    Id ( ev-left-formal-combination-subset-Ring
         ( mul-left-formal-combination-subset-Ring r u))
       ( mul-Ring R r (ev-left-formal-combination-subset-Ring u))
  preserves-mul-ev-left-formal-combination-subset-Ring r nil =
    inv (right-zero-law-mul-Ring R r)
  preserves-mul-ev-left-formal-combination-subset-Ring r (cons x u) =
    ( ap-add-Ring R
      ( associative-mul-Ring R r (pr1 x) (inclusion-subset-Ring R S (pr2 x)))
      ( preserves-mul-ev-left-formal-combination-subset-Ring r u)) ∙
    ( inv
      ( left-distributive-mul-add-Ring R r
        ( mul-Ring R (pr1 x) (inclusion-subset-Ring R S (pr2 x)))
        ( ev-left-formal-combination-subset-Ring u)))

  subset-left-ideal-subset-Ring' : type-Ring R → UU (l1 ⊔ l2)
  subset-left-ideal-subset-Ring' x =
    fib ev-left-formal-combination-subset-Ring x

  subset-left-ideal-subset-Ring : subset-Ring (l1 ⊔ l2) R
  subset-left-ideal-subset-Ring x =
    trunc-Prop (subset-left-ideal-subset-Ring' x)

  contains-zero-left-ideal-subset-Ring :
    contains-zero-subset-Ring R subset-left-ideal-subset-Ring
  contains-zero-left-ideal-subset-Ring = unit-trunc-Prop (pair nil refl)

  is-closed-under-addition-left-ideal-subset-Ring' :
    (x y : type-Ring R) →
    subset-left-ideal-subset-Ring' x → subset-left-ideal-subset-Ring' y →
    subset-left-ideal-subset-Ring' (add-Ring R x y)
  pr1
    ( is-closed-under-addition-left-ideal-subset-Ring' x y
      ( pair l p) (pair k q)) =
    concat-list l k
  pr2
    ( is-closed-under-addition-left-ideal-subset-Ring' x y
      ( pair l p) (pair k q)) =
    ( preserves-concat-ev-left-formal-combination-subset-Ring l k) ∙
    ( ap-add-Ring R p q)

  is-closed-under-addition-left-ideal-subset-Ring :
    is-closed-under-addition-subset-Ring R subset-left-ideal-subset-Ring
  is-closed-under-addition-left-ideal-subset-Ring x y H K =
    apply-universal-property-trunc-Prop H
      ( subset-left-ideal-subset-Ring (add-Ring R x y))
      ( λ H' →
        apply-universal-property-trunc-Prop K
          ( subset-left-ideal-subset-Ring (add-Ring R x y))
          ( λ K' →
            unit-trunc-Prop
              ( is-closed-under-addition-left-ideal-subset-Ring' x y H' K')))

  is-closed-under-left-multiplication-left-ideal-subset-Ring' :
    (r x : type-Ring R) → subset-left-ideal-subset-Ring' x →
    subset-left-ideal-subset-Ring' (mul-Ring R r x)
  pr1
    ( is-closed-under-left-multiplication-left-ideal-subset-Ring' r x
      ( pair l p)) =
    mul-left-formal-combination-subset-Ring r l
  pr2
    ( is-closed-under-left-multiplication-left-ideal-subset-Ring' r x
      ( pair l p)) =
    ( preserves-mul-ev-left-formal-combination-subset-Ring r l) ∙
    ( ap (mul-Ring R r) p)

  is-closed-under-left-multiplication-left-ideal-subset-Ring :
    is-closed-under-left-multiplication-subset-Ring R
      subset-left-ideal-subset-Ring
  is-closed-under-left-multiplication-left-ideal-subset-Ring r x H =
    apply-universal-property-trunc-Prop H
      ( subset-left-ideal-subset-Ring (mul-Ring R r x))
      ( λ H' →
        unit-trunc-Prop
          ( is-closed-under-left-multiplication-left-ideal-subset-Ring' r x H'))

  is-closed-under-negatives-left-ideal-subset-Ring :
    is-closed-under-negatives-subset-Ring R subset-left-ideal-subset-Ring
  is-closed-under-negatives-left-ideal-subset-Ring x H =
    tr ( type-Prop ∘ subset-left-ideal-subset-Ring)
       ( mul-neg-one-Ring R x)
       ( is-closed-under-left-multiplication-left-ideal-subset-Ring
         ( neg-one-Ring R)
         ( x)
         ( H))

  left-ideal-subset-Ring : left-ideal-Ring (l1 ⊔ l2) R
  pr1 left-ideal-subset-Ring = subset-left-ideal-subset-Ring
  pr1 (pr1 (pr2 left-ideal-subset-Ring)) = contains-zero-left-ideal-subset-Ring
  pr1 (pr2 (pr1 (pr2 left-ideal-subset-Ring))) =
    is-closed-under-addition-left-ideal-subset-Ring
  pr2 (pr2 (pr1 (pr2 left-ideal-subset-Ring))) =
    is-closed-under-negatives-left-ideal-subset-Ring
  pr2 (pr2 left-ideal-subset-Ring) =
    is-closed-under-left-multiplication-left-ideal-subset-Ring

  contains-subset-left-ideal-subset-Ring :
    S ⊆ subset-left-ideal-subset-Ring
  contains-subset-left-ideal-subset-Ring s H =
    unit-trunc-Prop
      ( pair
        ( cons (pair (one-Ring R) (pair s H)) nil)
        ( ( right-unit-law-add-Ring R (mul-Ring R (one-Ring R) s)) ∙
          ( left-unit-law-mul-Ring R s)))

  contains-left-formal-combinations-left-ideal-subset-Ring :
    {l3 : Level} (I : left-ideal-Ring l3 R) → S ⊆ subset-left-ideal-Ring R I →
    (x : left-formal-combination-subset-Ring) →
    is-in-left-ideal-Ring R I (ev-left-formal-combination-subset-Ring x)
  contains-left-formal-combinations-left-ideal-subset-Ring I H nil =
    contains-zero-left-ideal-Ring R I
  contains-left-formal-combinations-left-ideal-subset-Ring I H
    ( cons (pair r (pair s K)) c) =
    is-closed-under-addition-left-ideal-Ring R I
      ( mul-Ring R r s)
      ( ev-left-formal-combination-subset-Ring c)
      ( is-closed-under-left-multiplication-left-ideal-Ring R I r s (H s K))
      ( contains-left-formal-combinations-left-ideal-subset-Ring I H c)

  is-left-ideal-generated-by-subset-left-ideal-subset-Ring :
    (l : Level) →
    is-left-ideal-generated-by-subset-Ring R S
      ( left-ideal-subset-Ring)
      ( contains-subset-left-ideal-subset-Ring)
      ( l)
  is-left-ideal-generated-by-subset-left-ideal-subset-Ring l J K x H =
    apply-universal-property-trunc-Prop H (subset-left-ideal-Ring R J x) P
    where
    P : subset-left-ideal-subset-Ring' x → is-in-left-ideal-Ring R J x
    P (pair c refl) =
      contains-left-formal-combinations-left-ideal-subset-Ring J K c
```

### Construction of right ideals generated by a subset of a ring

```agda
module _
  {l1 l2 : Level} (R : Ring l1) (S : subset-Ring l2 R)
  where

  right-formal-combination-subset-Ring : UU (l1 ⊔ l2)
  right-formal-combination-subset-Ring =
    list (type-subset-Ring R S × type-Ring R)

  ev-right-formal-combination-subset-Ring :
    right-formal-combination-subset-Ring → type-Ring R
  ev-right-formal-combination-subset-Ring nil = zero-Ring R
  ev-right-formal-combination-subset-Ring (cons (pair s r) l) =
    add-Ring R
      ( mul-Ring R (inclusion-subset-Ring R S s) r)
      ( ev-right-formal-combination-subset-Ring l)

  preserves-concat-ev-right-formal-combination-subset-Ring :
    (u v : right-formal-combination-subset-Ring) →
    ev-right-formal-combination-subset-Ring (concat-list u v) ＝
    add-Ring R
      ( ev-right-formal-combination-subset-Ring u)
      ( ev-right-formal-combination-subset-Ring v)
  preserves-concat-ev-right-formal-combination-subset-Ring nil v =
    inv (left-unit-law-add-Ring R (ev-right-formal-combination-subset-Ring v))
  preserves-concat-ev-right-formal-combination-subset-Ring
    ( cons (pair s r) u) v =
    ( ap
      ( add-Ring R (mul-Ring R (inclusion-subset-Ring R S s) r))
      ( preserves-concat-ev-right-formal-combination-subset-Ring u v)) ∙
    ( inv
      ( associative-add-Ring R
        ( mul-Ring R (inclusion-subset-Ring R S s) r)
        ( ev-right-formal-combination-subset-Ring u)
        ( ev-right-formal-combination-subset-Ring v)))

  mul-right-formal-combination-subset-Ring :
    right-formal-combination-subset-Ring →
    type-Ring R → right-formal-combination-subset-Ring
  mul-right-formal-combination-subset-Ring l r =
    map-list (λ (x , y) → (x , (mul-Ring R y r))) l

  preserves-mul-ev-right-formal-combination-subset-Ring :
    (u : right-formal-combination-subset-Ring) (r : type-Ring R) →
    Id ( ev-right-formal-combination-subset-Ring
         ( mul-right-formal-combination-subset-Ring u r))
       ( mul-Ring R (ev-right-formal-combination-subset-Ring u) r)
  preserves-mul-ev-right-formal-combination-subset-Ring nil r =
    inv (left-zero-law-mul-Ring R r)
  preserves-mul-ev-right-formal-combination-subset-Ring (cons (x , s) u) r =
    ( ap-add-Ring R
      ( inv (associative-mul-Ring R (inclusion-subset-Ring R S x) s r))
      ( preserves-mul-ev-right-formal-combination-subset-Ring u r)) ∙
    ( inv
      ( right-distributive-mul-add-Ring R
        ( mul-Ring R (inclusion-subset-Ring R S x) s)
        ( ev-right-formal-combination-subset-Ring u)
        ( r)))

  subset-right-ideal-subset-Ring' : type-Ring R → UU (l1 ⊔ l2)
  subset-right-ideal-subset-Ring' x =
    fib ev-right-formal-combination-subset-Ring x

  subset-right-ideal-subset-Ring : subset-Ring (l1 ⊔ l2) R
  subset-right-ideal-subset-Ring x =
    trunc-Prop (subset-right-ideal-subset-Ring' x)

  contains-zero-right-ideal-subset-Ring :
    contains-zero-subset-Ring R subset-right-ideal-subset-Ring
  contains-zero-right-ideal-subset-Ring = unit-trunc-Prop (pair nil refl)

  is-closed-under-addition-right-ideal-subset-Ring' :
    (x y : type-Ring R) →
    subset-right-ideal-subset-Ring' x → subset-right-ideal-subset-Ring' y →
    subset-right-ideal-subset-Ring' (add-Ring R x y)
  pr1
    ( is-closed-under-addition-right-ideal-subset-Ring' x y (l , p) (k , q)) =
    concat-list l k
  pr2
    ( is-closed-under-addition-right-ideal-subset-Ring' x y (l , p) (k , q)) =
    ( preserves-concat-ev-right-formal-combination-subset-Ring l k) ∙
    ( ap-add-Ring R p q)

  is-closed-under-addition-right-ideal-subset-Ring :
    is-closed-under-addition-subset-Ring R subset-right-ideal-subset-Ring
  is-closed-under-addition-right-ideal-subset-Ring x y H K =
    apply-universal-property-trunc-Prop H
      ( subset-right-ideal-subset-Ring (add-Ring R x y))
      ( λ H' →
        apply-universal-property-trunc-Prop K
          ( subset-right-ideal-subset-Ring (add-Ring R x y))
          ( λ K' →
            unit-trunc-Prop
              ( is-closed-under-addition-right-ideal-subset-Ring' x y H' K')))

  is-closed-under-right-multiplication-right-ideal-subset-Ring' :
    (x r : type-Ring R) → subset-right-ideal-subset-Ring' x →
    subset-right-ideal-subset-Ring' (mul-Ring R x r)
  pr1
    ( is-closed-under-right-multiplication-right-ideal-subset-Ring' x r
      ( pair l p)) =
    mul-right-formal-combination-subset-Ring l r
  pr2
    ( is-closed-under-right-multiplication-right-ideal-subset-Ring' x r
      ( pair l p)) =
    ( preserves-mul-ev-right-formal-combination-subset-Ring l r) ∙
    ( ap (mul-Ring' R r) p)

  is-closed-under-right-multiplication-right-ideal-subset-Ring :
    is-closed-under-right-multiplication-subset-Ring R
      subset-right-ideal-subset-Ring
  is-closed-under-right-multiplication-right-ideal-subset-Ring x r H =
    apply-universal-property-trunc-Prop H
      ( subset-right-ideal-subset-Ring (mul-Ring R x r))
      ( λ H' →
        unit-trunc-Prop
          ( is-closed-under-right-multiplication-right-ideal-subset-Ring'
            x r H'))

  is-closed-under-negatives-right-ideal-subset-Ring :
    is-closed-under-negatives-subset-Ring R subset-right-ideal-subset-Ring
  is-closed-under-negatives-right-ideal-subset-Ring x H =
    tr ( type-Prop ∘ subset-right-ideal-subset-Ring)
       ( mul-neg-one-Ring' R x)
       ( is-closed-under-right-multiplication-right-ideal-subset-Ring
         ( x)
         ( neg-one-Ring R)
         ( H))

  right-ideal-subset-Ring : right-ideal-Ring (l1 ⊔ l2) R
  pr1 right-ideal-subset-Ring =
    subset-right-ideal-subset-Ring
  pr1 (pr1 (pr2 right-ideal-subset-Ring)) =
    contains-zero-right-ideal-subset-Ring
  pr1 (pr2 (pr1 (pr2 right-ideal-subset-Ring))) =
    is-closed-under-addition-right-ideal-subset-Ring
  pr2 (pr2 (pr1 (pr2 right-ideal-subset-Ring))) =
    is-closed-under-negatives-right-ideal-subset-Ring
  pr2 (pr2 right-ideal-subset-Ring) =
    is-closed-under-right-multiplication-right-ideal-subset-Ring

  contains-subset-right-ideal-subset-Ring :
    S ⊆ subset-right-ideal-subset-Ring
  contains-subset-right-ideal-subset-Ring s H =
    unit-trunc-Prop
      ( ( cons ((s , H) , one-Ring R) nil) ,
        ( ( right-unit-law-add-Ring R (mul-Ring R s (one-Ring R))) ∙
          ( right-unit-law-mul-Ring R s)))

  contains-right-formal-combinations-right-ideal-subset-Ring :
    {l3 : Level} (I : right-ideal-Ring l3 R) → S ⊆ subset-right-ideal-Ring R I →
    (x : right-formal-combination-subset-Ring) →
    is-in-right-ideal-Ring R I (ev-right-formal-combination-subset-Ring x)
  contains-right-formal-combinations-right-ideal-subset-Ring I H nil =
    contains-zero-right-ideal-Ring R I
  contains-right-formal-combinations-right-ideal-subset-Ring I H
    ( cons (pair (pair s K) r) c) =
    is-closed-under-addition-right-ideal-Ring R I
      ( mul-Ring R s r)
      ( ev-right-formal-combination-subset-Ring c)
      ( is-closed-under-right-multiplication-right-ideal-Ring R I s r (H s K))
      ( contains-right-formal-combinations-right-ideal-subset-Ring I H c)

  is-right-ideal-generated-by-subset-right-ideal-subset-Ring :
    (l : Level) →
    is-right-ideal-generated-by-subset-Ring R S
      ( right-ideal-subset-Ring)
      ( contains-subset-right-ideal-subset-Ring)
      ( l)
  is-right-ideal-generated-by-subset-right-ideal-subset-Ring l J K x H =
    apply-universal-property-trunc-Prop H (subset-right-ideal-Ring R J x) P
    where
    P : subset-right-ideal-subset-Ring' x → is-in-right-ideal-Ring R J x
    P (pair c refl) =
      contains-right-formal-combinations-right-ideal-subset-Ring J K c
```

### Construction of ideals generated by subsets

```agda
module _
  {l1 l2 : Level} (R : Ring l1) (S : subset-Ring l2 R)
  where

  formal-combination-subset-Ring : UU (l1 ⊔ l2)
  formal-combination-subset-Ring =
    list (type-Ring R × (type-subset-Ring R S × type-Ring R))

  ev-formal-combination-subset-Ring :
    formal-combination-subset-Ring → type-Ring R
  ev-formal-combination-subset-Ring nil = zero-Ring R
  ev-formal-combination-subset-Ring (cons (x , s , y) l) =
    add-Ring R
      ( mul-Ring R (mul-Ring R x (inclusion-subset-Ring R S s)) y)
      ( ev-formal-combination-subset-Ring l)

  preserves-concat-ev-formal-combination-subset-Ring :
    (u v : formal-combination-subset-Ring) →
    ev-formal-combination-subset-Ring (concat-list u v) ＝
    add-Ring R
      ( ev-formal-combination-subset-Ring u)
      ( ev-formal-combination-subset-Ring v)
  preserves-concat-ev-formal-combination-subset-Ring nil v =
    inv (left-unit-law-add-Ring R (ev-formal-combination-subset-Ring v))
  preserves-concat-ev-formal-combination-subset-Ring (cons (x , s , y) u) v =
    ( ap
      ( add-Ring R
        ( mul-Ring R (mul-Ring R x (inclusion-subset-Ring R S s)) y))
      ( preserves-concat-ev-formal-combination-subset-Ring u v)) ∙
    ( inv
      ( associative-add-Ring R
        ( mul-Ring R (mul-Ring R x (inclusion-subset-Ring R S s)) y)
        ( ev-formal-combination-subset-Ring u)
        ( ev-formal-combination-subset-Ring v)))

  mul-formal-combination-subset-Ring :
    type-Ring R → formal-combination-subset-Ring →
    type-Ring R → formal-combination-subset-Ring
  mul-formal-combination-subset-Ring x l y =
    map-list (λ (u , s , v) → (mul-Ring R x u , s , mul-Ring R v y)) l

  preserves-mul-ev-formal-combination-subset-Ring :
    (x : type-Ring R) (u : formal-combination-subset-Ring) (y : type-Ring R) →
    ( ev-formal-combination-subset-Ring
      ( mul-formal-combination-subset-Ring x u y)) ＝
    ( mul-Ring R (mul-Ring R x (ev-formal-combination-subset-Ring u)) y)
  preserves-mul-ev-formal-combination-subset-Ring x nil y =
    inv
      ( ( ap
          ( mul-Ring' R y)
          ( right-zero-law-mul-Ring R x)) ∙
        ( left-zero-law-mul-Ring R y))
  preserves-mul-ev-formal-combination-subset-Ring x
    ( cons (r , (s , H) , t) u) y =
    ( ap-add-Ring R
      ( ( inv
          ( associative-mul-Ring R
            ( mul-Ring R (mul-Ring R x r) s)
            ( t)
            ( y))) ∙
        ( ap
          ( mul-Ring' R y)
          ( ( ap (mul-Ring' R t) (associative-mul-Ring R x r s)) ∙
            ( associative-mul-Ring R x (mul-Ring R r s) t))))
      ( preserves-mul-ev-formal-combination-subset-Ring x u y)) ∙
    ( ( inv
        ( right-distributive-mul-add-Ring R
          ( mul-Ring R x
            ( mul-Ring R (mul-Ring R r s) t))
          ( mul-Ring R x (ev-formal-combination-subset-Ring u))
          ( y))) ∙
      ( ap
        ( mul-Ring' R y)
        ( inv
          ( left-distributive-mul-add-Ring R x
            ( mul-Ring R (mul-Ring R r s) t)
            ( ev-formal-combination-subset-Ring u)))))

  subset-ideal-subset-Ring' : type-Ring R → UU (l1 ⊔ l2)
  subset-ideal-subset-Ring' x =
    fib ev-formal-combination-subset-Ring x

  subset-ideal-subset-Ring : subset-Ring (l1 ⊔ l2) R
  subset-ideal-subset-Ring x =
    trunc-Prop (subset-ideal-subset-Ring' x)

  is-in-ideal-subset-Ring : type-Ring R → UU (l1 ⊔ l2)
  is-in-ideal-subset-Ring = is-in-subtype subset-ideal-subset-Ring

  contains-zero-ideal-subset-Ring :
    contains-zero-subset-Ring R subset-ideal-subset-Ring
  contains-zero-ideal-subset-Ring = unit-trunc-Prop (pair nil refl)

  is-closed-under-addition-ideal-subset-Ring' :
    (x y : type-Ring R) →
    subset-ideal-subset-Ring' x → subset-ideal-subset-Ring' y →
    subset-ideal-subset-Ring' (add-Ring R x y)
  pr1 (is-closed-under-addition-ideal-subset-Ring' x y (l , H) (k , K)) =
    concat-list l k
  pr2 (is-closed-under-addition-ideal-subset-Ring' x y (l , H) (k , K)) =
    ( preserves-concat-ev-formal-combination-subset-Ring l k) ∙
    ( ap-add-Ring R H K)

  is-closed-under-addition-ideal-subset-Ring :
    is-closed-under-addition-subset-Ring R subset-ideal-subset-Ring
  is-closed-under-addition-ideal-subset-Ring x y H K =
    apply-universal-property-trunc-Prop H
      ( subset-ideal-subset-Ring (add-Ring R x y))
      ( λ H' →
        apply-universal-property-trunc-Prop K
          ( subset-ideal-subset-Ring (add-Ring R x y))
          ( λ K' →
            unit-trunc-Prop
              ( is-closed-under-addition-ideal-subset-Ring' x y H' K')))

  is-closed-under-multiplication-ideal-subset-Ring' :
    (r s t : type-Ring R) → subset-ideal-subset-Ring' s →
    subset-ideal-subset-Ring' (mul-Ring R (mul-Ring R r s) t)
  pr1 (is-closed-under-multiplication-ideal-subset-Ring' r s t (pair l p)) =
    mul-formal-combination-subset-Ring r l t
  pr2 (is-closed-under-multiplication-ideal-subset-Ring' r s t (pair l p)) =
    ( preserves-mul-ev-formal-combination-subset-Ring r l t) ∙
    ( ap (λ u → mul-Ring R (mul-Ring R r u) t) p)

  is-closed-under-left-multiplication-ideal-subset-Ring :
    is-closed-under-left-multiplication-subset-Ring R subset-ideal-subset-Ring
  is-closed-under-left-multiplication-ideal-subset-Ring x r H =
    apply-universal-property-trunc-Prop H
      ( subset-ideal-subset-Ring (mul-Ring R x r))
      ( λ H' →
        unit-trunc-Prop
          ( tr
            ( subset-ideal-subset-Ring')
            ( right-unit-law-mul-Ring R (mul-Ring R x r))
            ( is-closed-under-multiplication-ideal-subset-Ring' x r
              ( one-Ring R)
              ( H'))))

  is-closed-under-right-multiplication-ideal-subset-Ring :
    is-closed-under-right-multiplication-subset-Ring R subset-ideal-subset-Ring
  is-closed-under-right-multiplication-ideal-subset-Ring x r H =
    apply-universal-property-trunc-Prop H
      ( subset-ideal-subset-Ring (mul-Ring R x r))
      ( λ H' →
        unit-trunc-Prop
          ( tr
            ( subset-ideal-subset-Ring')
            ( ap
              ( mul-Ring' R r)
              ( left-unit-law-mul-Ring R x))
            ( is-closed-under-multiplication-ideal-subset-Ring'
              ( one-Ring R)
              ( x)
              ( r)
              ( H'))))

  is-closed-under-negatives-ideal-subset-Ring :
    is-closed-under-negatives-subset-Ring R subset-ideal-subset-Ring
  is-closed-under-negatives-ideal-subset-Ring x H =
    tr ( is-in-ideal-subset-Ring)
       ( mul-neg-one-Ring R x)
       ( is-closed-under-left-multiplication-ideal-subset-Ring
         ( neg-one-Ring R)
         ( x)
         ( H))

  ideal-subset-Ring : ideal-Ring (l1 ⊔ l2) R
  pr1 ideal-subset-Ring =
    subset-ideal-subset-Ring
  pr1 (pr1 (pr2 ideal-subset-Ring)) =
    contains-zero-ideal-subset-Ring
  pr1 (pr2 (pr1 (pr2 ideal-subset-Ring))) =
    is-closed-under-addition-ideal-subset-Ring
  pr2 (pr2 (pr1 (pr2 ideal-subset-Ring))) =
    is-closed-under-negatives-ideal-subset-Ring
  pr1 (pr2 (pr2 ideal-subset-Ring)) =
    is-closed-under-left-multiplication-ideal-subset-Ring
  pr2 (pr2 (pr2 ideal-subset-Ring)) =
    is-closed-under-right-multiplication-ideal-subset-Ring

  contains-subset-ideal-subset-Ring :
    S ⊆ subset-ideal-subset-Ring
  contains-subset-ideal-subset-Ring s H =
    unit-trunc-Prop
      ( ( cons (one-Ring R , (s , H) , one-Ring R) nil) ,
        ( ( right-unit-law-add-Ring R _) ∙
          ( right-unit-law-mul-Ring R _ ∙ left-unit-law-mul-Ring R _)))

  contains-formal-combinations-ideal-subset-Ring :
    {l3 : Level} (I : ideal-Ring l3 R) → S ⊆ subset-ideal-Ring R I →
    (x : formal-combination-subset-Ring) →
    is-in-ideal-Ring R I (ev-formal-combination-subset-Ring x)
  contains-formal-combinations-ideal-subset-Ring I H nil =
    contains-zero-ideal-Ring R I
  contains-formal-combinations-ideal-subset-Ring I H
    ( cons (r , (pair s K) , t) c) =
    is-closed-under-addition-ideal-Ring R I
      ( mul-Ring R (mul-Ring R r s) t)
      ( ev-formal-combination-subset-Ring c)
      ( is-closed-under-right-multiplication-ideal-Ring R I
        ( mul-Ring R r s)
        ( t)
        ( is-closed-under-left-multiplication-ideal-Ring R I r s (H s K)))
      ( contains-formal-combinations-ideal-subset-Ring I H c)

  is-ideal-generated-by-subset-ideal-subset-Ring :
    {l : Level} →
    is-ideal-generated-by-subset-Ring R S
      ( ideal-subset-Ring)
      ( contains-subset-ideal-subset-Ring)
      ( l)
  is-ideal-generated-by-subset-ideal-subset-Ring J K x H =
    apply-universal-property-trunc-Prop H (subset-ideal-Ring R J x) P
    where
    P : subset-ideal-subset-Ring' x → is-in-ideal-Ring R J x
    P (pair c refl) =
      contains-formal-combinations-ideal-subset-Ring J K c
```
